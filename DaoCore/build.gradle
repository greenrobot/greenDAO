apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'de.greenrobot'
version = '1.3.0-beta-1'
sourceCompatibility = 1.6

def isSnapshot = version.endsWith('-SNAPSHOT')
def sonatypeRepositoryUrl
if(isSnapshot) {
	sonatypeRepositoryUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
} else {
	sonatypeRepositoryUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
}

repositories {
    mavenCentral()
}

dependencies {
    compile('com.google.android:android:4.1.1.4')
    compile('com.google.android:android-test:4.1.1.4')
    compile('com.google.android:annotations:4.1.1.4')
    compile('com.google.android:support-v4:r7')
}

sourceSets {
    main {
        java {
            srcDir 'src'
        }
    }
}

javadoc {
    title = " greenDAO ${version} API"
	options.bottom = 'Available under the Apache License, Version 2.0 - <i>Copyright &#169; 2011-2013 <a href="http://greenrobot.de/">greenrobot.de</a>. All Rights Reserved.</i>'
	excludes = ['de/greenrobot/dao/internal','de/greenrobot/dao/Internal*']
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            // if(! isSnapshot) {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
            // }

            // We must check if authentication info is available
            if(project.hasProperty('sonatypeUsername') && project.hasProperty('sonatypePassword')) {
                repository(url: sonatypeRepositoryUrl) {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }
            } else {
                println 'No settings for sonatypeUsername/sonatypePassword'
            }

            pom.project {
               name 'greenDAO'
               packaging 'jar'
               description 'greenDAO is a light and fast ORM for Android'
               url 'http://greendao-orm.com'

               scm {
                   url 'https://github.com/greenrobot/greenDAO'
                   connection 'scm:git@github.com:greenrobot/greenDAO.git'
                   developerConnection 'scm:git@github.com:greenrobot/greenDAO.git'
               }

               licenses {
                   license {
                       name 'The Apache Software License, Version 2.0'
                       url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                       distribution 'repo'
                   }
               }

               developers {
                   developer {
                       id 'greenrobot'
                       name 'greenrobot'
                   }
               }
               
               issueManagement {
                   system 'GitHub Issues'
                   url 'https://github.com/greenrobot/greenDAO/issues'
               }
               
               organization {
                   name 'greenrobot'
                   url 'http://greenrobot.de'
               }
           }
        }
    }
}